buildscript {
    project.ext.KOTLIN_VERSION = '1.2.51'

    repositories {
        mavenCentral()
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.android.tools.lint:lint-gradle:26.1.3'
        classpath 'com.noveogroup.android:check:1.2.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.noveogroup.android.check'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId 'com.quittle.rds3'
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        disable 'GradleDependency', 'AllowBackup', 'GoogleAppIndexingWarning'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        allWarningsAsErrors = true
    }
}

check {
    abortOnError true
    checkstyle { config hard() }
    findbugs { config hard() }
    pmd { config hard() }
}

dependencies {
    api 'com.android.support:support-v4:27.1.1'
    api 'com.google.guava:guava:20.0'
    api "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
}

defaultTasks 'check', 'assemble'
