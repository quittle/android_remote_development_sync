buildscript {
    project.ext.KOTLIN_VERSION = '1.2.70'

    repositories {
        mavenCentral()
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.android.tools.lint:lint-gradle:26.1.4'
        classpath 'com.noveogroup.android:check:1.2.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.noveogroup.android.check'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId 'com.quittle.rds3'
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        disable 'GradleDependency', 'AllowBackup', 'GoogleAppIndexingWarning'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        allWarningsAsErrors = true
    }
}

check {
    abortOnError true
    checkstyle { config hard() }
    findbugs { config hard() }
    pmd { config hard() }
}

dependencies {
    api 'com.android.support:support-v4:28.0.0'
    api 'com.google.guava:guava:20.0'
    api "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
}

defaultTasks 'check', 'assemble'
